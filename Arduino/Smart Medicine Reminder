Arduino Code:
#include <Wire.h>
#include <RTClib.h>
#include <LiquidCrystal_I2C.h>

// RTC & LCD
RTC_DS3231 rtc;
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Pin Definitions
const int buzzer = 8;
const int led1 = 9;
const int led2 = 10;
const int buttonPin = 6;
const int missedSignalPin = 7;   // To ESP D7
const int refillSignalPin = 5;   // To ESP D5
 


// Alarm Times
const int alarm1Hour = 10;
const int alarm1Minute =39;
const int alarm2Hour = 10;
const int alarm2Minute =00;

// Medicine Tracking

int medicineCount = 3;
const int refillThreshold = 2;

void setup() {
  Wire.begin();
  lcd.begin(16, 2);
  lcd.backlight();

  if (!rtc.begin()) {
    lcd.print("RTC not found!");
    while (1);
  }

  // Uncomment once to set the date/time
 // rtc.adjust(DateTime(2025, 8, 23, 10,30 , 0));

  pinMode(buzzer, OUTPUT);
  pinMode(led1, OUTPUT);
  pinMode(led2, OUTPUT);
  pinMode(buttonPin, INPUT_PULLUP);
  pinMode(missedSignalPin, OUTPUT);
  pinMode(refillSignalPin, OUTPUT);

  digitalWrite(missedSignalPin, LOW);
  digitalWrite(refillSignalPin, LOW);

  lcd.setCursor(0, 0);
  lcd.print("Date & Time:");
  delay(2000);
  lcd.clear();
}

void loop() {
  DateTime now = rtc.now();

  // Show Date
  lcd.setCursor(0, 0);
  lcd.print("Date: ");
  if (now.day() < 10) lcd.print("0");
  lcd.print(now.day()); lcd.print("/");
  if (now.month() < 10) lcd.print("0");
  lcd.print(now.month()); lcd.print("/");
  lcd.print(now.year());

  // Show Time
  lcd.setCursor(0, 1);
  lcd.print("Time: ");
  if (now.hour() < 10) lcd.print("0");
  lcd.print(now.hour()); lcd.print(":");
  if (now.minute() < 10) lcd.print("0");
  lcd.print(now.minute()); lcd.print(":");
  if (now.second() < 10) lcd.print("0");
  lcd.print(now.second());

  // --- Alarm 1 ---
  if (now.hour() == alarm1Hour && now.minute() == alarm1Minute && now.second() == 0) {
    triggerAlarm(led1);
  }

  // --- Alarm 2 ---
  if (now.hour() == alarm2Hour && now.minute() == alarm2Minute && now.second() == 0) {
    triggerAlarm(led2);
  }

  delay(1000); // Refresh every second
}

void triggerAlarm(int ledPin) {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Take Medicine!");

  digitalWrite(ledPin, HIGH);
  tone(buzzer, 1000);

  bool taken = false;
  unsigned long startTime = millis();

  // Wait 30 seconds for button press
  while (millis() - startTime < 30000) {
    if (digitalRead(buttonPin) == LOW) {
      taken = true;
      break;
    }
  }

  digitalWrite(ledPin, LOW);
  noTone(buzzer);

  if (taken) {
    medicineCount--;

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Taken. Left: ");
    lcd.print(medicineCount);
    delay(2000);

    // Refill check
    if (medicineCount <= refillThreshold) {
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Refill Needed!");

      digitalWrite(refillSignalPin, HIGH);
      delay(1000);
      digitalWrite(refillSignalPin, LOW);
    }

  } else {
    // Missed medicine alert
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Missed Dose!");

    digitalWrite(missedSignalPin, HIGH);
    delay(1000);
    digitalWrite(missedSignalPin, LOW);
  }

  delay(1000); // small buffer
}
